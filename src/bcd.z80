;
; Binary to decimal (8-bit)
; Copyright 2018 Damian Yerrick
; 
; This software is provided 'as-is', without any express or implied
; warranty.  In no event will the authors be held liable for any damages
; arising from the use of this software.
; 
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
; 
; 1. The origin of this software must not be misrepresented; you must not
;    claim that you wrote the original software. If you use this software
;    in a product, an acknowledgment in the product documentation would be
;    appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
;    misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;
section "bcd",ROM0

bcd8bit_iter: macro
  cp \1
  jr c,.nope\@
  sub \1
.nope\@:
  rl b
  endm

;;
; Converts an 8-bit value to decimal.
; @param A the value
; @return A: hundreds and tens digits; C: ones digit;
; DEHL: unchanged; Z set if number is less than 10
bcd8bit::
  bcd8bit_iter 200
  bcd8bit_iter 100
  bcd8bit_iter 80
  bcd8bit_iter 40
  bcd8bit_iter 20
  bcd8bit_iter 10
  ld c,a
  ld a,b
  cpl  ; correct for 8080 use of borrow flag instead of carry
  and $3F
  ret

;;
; Calculates one digit of converting a fraction to a percentage.
; @param B numerator, less than C
; @param C denominator
; @return A = floor(10 * B / C); B = 10 * B % C;
; CHL unchanged; D clobbered; E = 0
pctdigit::
  ld de,$1000

  ; bit 3: A.E = B * 1.25
  ld a,b
  srl a
  rr e
  srl a
  rr e
  adc b
  jr c,.yessub3
  cp c
  jr c,.nosub3
  .yessub3:
    sub c
    or a
  .nosub3:
  rl d

  ; bits 2-0: mul A.E by 2
  .bit210loop:
    rl e
    adc a
    jr c,.yessub210
    cp c
    jr c,.nosub210
    .yessub210:
      sub c
      or a
    .nosub210:
    rl d
    jr nc,.bit210loop

  ld b,a
  ; Binary to decimal subtracts if trial subtraction has no borrow.
  ; 6502/ARM carry: 0: borrow; 1: no borrow
  ; 8080 carry: 1: borrow; 0: borrow
  ; The 6502 interpretation is more convenient for binary to decimal
  ; conversion, so convert to 6502 discipline
  ld a,$0F
  xor d
  ret
