include "src/hardware.inc"
include "src/global.inc"

section "mainvars", WRAM0
cur_level: ds 1
attract_mode: ds 1

section "main", ROM0

timer_handler::
serial_handler::
joypad_handler::
  reti

main::
  ; Set up vblank handler
  ld a,IEF_VBLANK
  ldh [rIE],a  ; enable IRQs
  xor a
  ldh [rIF],a  ; Acknowledge any pending IRQs
  ld [nmis],a
  ei

  call audio_init
  ld a,0
  call audio_play_fx

  call do_instructions
  ld a,[new_keys]
  and PADF_SELECT
  ld [attract_mode],a

  call lcd_off
  call load_floor_chr
  call load_statusbar_chr
  ld hl,CHRRAM0
  ld de,Libbet_pb16
  ld b,43
  call pb16_unpack_block
  ld bc,2
  call srand
  call run_dma  ; shut up BGB
  xor a
  ld [cur_level],a

another_floor:
  if RANDOMIZE_SEED
    call rand
    ld a,[nmis]
    xor b
    ld b,a
    call srand
  endc

  ld a,[attract_mode]
  or a
  jr nz,.load_attract_floor

    ; Generate the floor
    ld a,[cur_level]
    ld de,level_floor_sizes
    call de_index_a
    if FORCE_FLOOR_SIZE
      ld hl,FORCE_FLOOR_SIZE
    endc
    ld a,l
    ld [floor_width],a
    ld a,h
    ld [floor_height],a
    call make_good_floor
    call calc_floor_top_row
    jr .have_floor_layout
  .load_attract_floor:
    ld bc,$0404
    ld hl,attract_map
    call make_prefab_floor
    ld a,3
    ld [floor_top_row],a
  .have_floor_layout:

  xor a
  ld [cur_score],a
  call place_exit

  ; Clear the map
  call lcd_off
  call popslide_init
  ld de,_SCRN0
  ld bc,32*16
  ld h,$6F  ; background tile
  call memset
  call draw_whole_floor
  call cursor_to_home
  call init_player_pos
  call draw_floor_border
  call prepare_exit_door  ; leave exit door in popslide buffer
  call draw_statusbar_tilemap  ; leave score in popslide buffer

  ld a,[attract_mode]
  or a
  jr z,.not_attract_vram
    call attract_init_vram
  .not_attract_vram:

  call fade_in

  ld a,%00011111
  ldh [rOBP0],a
  ld a,LCDCF_ON|LCDCF_BG9800|LCDCF_BG8800|LCDCF_BGON|LCDCF_OBJON
  ld [vblank_lcdc_value],a
  ldh [rLCDC],a

forever:
  call move_player_anim
  xor a
  ld [oam_used],a
  call draw_player
  call lcd_clear_oam
  call fade_update

  call wait_vblank_irq
  call run_dma
  call popslide_terminate_blit
  ld a,[fade_bgp]
  ldh [rBGP],a
  call audio_update

  call read_pad
  ld a,[attract_mode]
  or a
  jr z,.not_attract_startcheck
    ld a,[new_keys]
    bit PADB_START,A
    jp nz,main
  .not_attract_startcheck:

  call check_exit_pct
  jr nc,.exit_not_opened
    ld a,6
    call audio_play_fx
    call prepare_exit_door
    call fade_in
  .exit_not_opened:

  call player_can_start_roll
  jr z,.not_begin_move

    ; cursor Y = 255 means Libbet is on the exit door
    ld a,[cursor_y]
    inc a
    jr nz,.not_next_floor
      ; Exit door in attract mode means return to menu
      ld a,[attract_mode]
      or a
      jr z,.next_floor
      jp main
    .not_next_floor:

    ; Sitting on a dead end after last move?
    call floor_read_at_cursor
    bit FLOORB_DEADEND,a
    jr z,.not_on_deadend
      set FLOORB_ENTERED,a
      call prepare_hDrawCell
      call player_start_fall
      ld a,3
      call audio_play_fx
      call cursor_to_home
      jr .not_begin_move
    .not_on_deadend:

    ld a,[attract_mode]
    or a
    jr z,.not_attract_keyread
      call attract_update
      bit PADB_START,a
      jr z, .have_keys
      jp main
    .not_attract_keyread:
      ld a,[cur_keys]
    .have_keys:
    call try_move
  .not_begin_move:

  if SELECT_RESTARTS
  ld a,[new_keys]
  bit PADB_SELECT, a
  jp nz,another_floor
  endc

  jp forever

.next_floor:
  ld a,[cur_level]
  inc a
  cp (level_floor_sizes_end - level_floor_sizes) / 2
  jp nc,main
  ld [cur_level],a
  jp another_floor

try_move:
  ld b,a
  and PADF_RIGHT|PADF_LEFT|PADF_UP|PADF_DOWN
  ret z

  ld c,$FF
  swap a
  .ctzloop:
    inc c
    rra
    jr nc,.ctzloop
  ld a,c
  ld [cursor_facing],a  ; Face the pressed direction

  ld a,b
  bit PADB_A,a  ; key A
  jr z,.notjump
    set 2,c  ; mark
  .notjump:
  push bc
  ld a,c
  call is_valid_move
  pop bc
  jr c,.have_valid_move

    ; Check whether it's a roll up from the top row
    ; while the exit is open
    ld a,2  ; roll up
    xor c
    ret nz
    call check_exitable_cell
    ret nc

    ; Mark the level as exited
    ld c,2
    call player_start_roll
    ld a,$FF
    ld [cursor_y],a
    jp fade_out
  .have_valid_move:

  push af
  push hl
  push de
  call player_start_roll
  pop de
  pop hl
  pop af

  ; A: 1 for color change (possibly scoring); 0 for not
  ; C: move attempted; E[5:3]: new Y; E[2:0]: new X;
  ; HL: pointer to destination cell
  or a
  jr z,.move_to_cell_e
    push de
    push hl

    ; Get which track bit corresponds to this move ID
    ld a,c
    and $03
     ld b,a
    inc b
    ld a,$08
    .shlloop:
      rla
      dec b
      jr nz,.shlloop
    ld b,a

    ; See if that track bit is set
    call floor_read_at_cursor
    ld a,[hl]
    cpl
    and b
    jr z,.already_have_track_bit
      or [hl]
      ld [hl],a
      ld a,5
      call audio_play_fx  ; score point sound
      ld hl,cur_score
      ld b,[hl]
      inc b
      ld [hl],b
      ld a,[max_score]
      cp b
      jr nz,.not_100_percented
        ld a,6
        call audio_play_fx
        call fade_in
      .not_100_percented:
      call prepare_hDrawCell
      call prepare_pctage
    .already_have_track_bit:
    pop hl
    pop de

    ; Mark as entered
    set 3,[hl]
  .move_to_cell_e:
  ld a,e
  rra
  rra
  rra
  and $07
  ld [cursor_y],a
  ld a,e
  and $07
  ld [cursor_x],a
  ret

Libbet_pb16:
  incbin "obj/gb/Libbet.chrgb.pb16"

level_floor_sizes:
  db 2, 8
  db 4, 4
  db 6, 4
  db 6, 6
  db 8, 6
level_floor_sizes_end:
