;
; Background drawing for GB port of Magic Floor
;
; Copyright 2002, 2012 Martin Korth [concept]
; Copyright 2018, 2020 Damian Yerrick [program]
;
; This software is provided 'as-is', without any express or implied
; warranty.  In no event will the authors be held liable for any damages
; arising from the use of this software.
; 
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
; 
; 1. The origin of this software must not be misrepresented; you must not
;    claim that you wrote the original software. If you use this software
;    in a product, an acknowledgment in the product documentation would be
;    appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
;    misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;
include "src/hardware.inc"
include "src/global.inc"

section "floorupdate_wram",WRAM0
floor_top_row:: ds 1

section "load_floor_chr",ROM0

; Use tilemap as scratch space for unpacking 
decomppos equ $8800
harrow_chr equ decomppos+16*16
varrow_chr equ decomppos+20*16
deadend_chr equ decomppos+24*16

;;
; Convert all pixels with color 2 to color 1, in place
; Useful to modify tiles for Super Game Boy colorization.
; @param HL source and destination
; @param B sliver count (e.g. 16 for two tiles)
; @return AC trashed, B=0
color2to1_if_sgb:
  ld a, [hw_capability]
  rra
  ret nc
color2to1:
  ld a, [hl+]
  ld c, a      ; E: plane 0
  ld a, [hl-]  ; A: plane 1
  or c
  ld [hl+], a
  ld a, [hl]   ; A: plane 1
  and c
  ld [hl+], a
  dec b
  jr nz, color2to1
  ret

;;
; Copies C bytes from HL to DE with bit reversal
memcpy_bitreverse:
  ld a,[hl+]
  ld b,a
  rlca          ; 6 5 4 3 2 1 0 7
  rlca          ; 5 4 3 2 1 0 7 6
  xor b         ; 7564534231201706
  and $55       ; __64__42__20__06
  xor b         ; 7 4 5 2 3 0 1 6
  ld b,a
  swap a        ; 3 0 1 6 7 4 5 2
  xor b         ; 7340512637041562
  and $66       ; __0415____0415__
  xor b         ; 7 0 1 2 3 4 5 6
  rlca          ; 0 1 2 3 4 5 6 7
  ld [de], a
  inc de
  dec c
  jr nz,memcpy_bitreverse
  ret

;;
; Overlay C slivers at HL onto DE (modified in place)
; Color 0, 2, 3 unchanged; color 1 transparent
overlay_hl_onto_de:
  ; Bit plane 0 is clear for colors 0 and 2 (definitely opaque)
  ; and set for colors 1 and 3 (possibly transparent)
  ld a, [hl+]
  cpl     ; A=1 are possibly transparent
  ; Bit plane 1 is set for colors 2 and 3 (definitely opaque)
  ; and clear for colors 0 and 1 (possibly transparent)
  or [hl]
  cpl     ; A=0 for opaque, 1 for transparent
  dec hl
  ld b, a

  ; do plane 0
  ld a, [de]
  xor [hl]
  and b     ; clear out the opaque bits
  xor [hl]  ; so that they can become the new bits
  ld [de], a
  inc de
  inc hl

  ; do plane 1  
  ld a, [de]
  xor [hl]
  and b
  xor [hl]
  ld [de], a
  inc de
  inc hl
  dec c
  jr nz, overlay_hl_onto_de
  ret

overlay4:
  ld bc, $0420
  .loop:
    push bc
    push hl
    call overlay_hl_onto_de
    pop hl
    pop bc
    dec b
    jr nz, .loop
  ret

;;
; Load the CHR
load_floor_chr::
  ; Stage 1: Unpack
  ld de,floortiles_pb16
  ld hl,$9400  ; decompress here first; will be overwritten later
  push hl
  ld b, 16
  call pb16_unpack_block
  pop hl

  ; Generate right halves of symmetric tiles.
  ; I tried generating bottom halves as well but vertical flipping
  ; saved less than just repeating the (compressed) tiles in ROM.
  ld de, CHRRAM1
  ld bc, floormirrortypes
  ; HL: source tiles; DE: reflected tiles; BC: mirroring instructions
  .mirroringloop:
    ld a, [bc]
    or a
    jr z, .mirroringdone
    push bc
    ld bc, $40
    dec a
    jr nz, .mirrornotnone
      call memcpy
      jr .mirrornexttile
    .mirrornotnone:
      push hl
      ld c, $20
      call memcpy
      pop hl
      ld c, $20
      call memcpy_bitreverse
    .mirrornexttile:
    pop bc
    inc bc
    jr .mirroringloop
  .mirroringdone:

  ; If SGB, convert color 2 to color 1 in darkest, dark, light tiles
  ld hl, CHRRAM1
  ld b, 32*3
  call color2to1_if_sgb

  ; Now we have normal copies at tiles 128-143 ($8800-$88FF)
  ; and overlays at tiles 144-171 ($8900-$89BF).
  ; Produce these versions:
  ; Normal ($9000-$900F)
  ; Horizontal arrow overlaid ($9100-$91FF)
  ; Vertical arrow overlaid ($9200-$93FF)
  ; Hole overlaid ($9400-$94FF)
  ; Because the overlay operates in place due to register pressure),
  ; copy the normal tile into $9000, $9100, and $9400 in advance.
  ld hl, CHRRAM1
  ld de, CHRRAM2
  ld bc, 256
  call memcpy
  dec h  ; ld h, high(CHRRAM2)
  inc b  ; ld b, high(256)
  call memcpy
  dec h  ; ld h, high(CHRRAM2)
  ld d, high(CHRRAM2 + $400)
  inc b  ; ld b, high(256)
  call memcpy

  ld de, CHRRAM2+$0100
  ld hl, harrow_chr
  call overlay4
  push de
  ld hl, CHRRAM2
  ld bc, 512
  call memcpy
  pop de
  ld hl, varrow_chr
  call overlay4
  call overlay4
  push de
  ld hl, deadend_chr
  call overlay4
  pop hl

  ; Convert color 2 to color 1 in all hole tiles if on SGB
  ld b, $80
  call color2to1_if_sgb

  ; Finish by loading the border tiles into the area where
  ; decompression was performed
  ld de,floorborder_pb16
  ld a, [hw_capability]
  rra
  jr nc, .border_not_sgb
    ld de, floorborder_sgb_pb16
  .border_not_sgb:

  ld hl,CHRRAM2+$6C*16
  ld b,20
  jp pb16_unpack_block

section "floor_chr", ROM0
floortiles_pb16: incbin "obj/gb/floorpieces-h.2bpp.pb16"
floorborder_pb16: incbin "obj/gb/floorborder-h.2bpp.pb16"
floorborder_sgb_pb16: incbin "obj/gb/floorborder-sgb-h.2bpp.pb16"
floormirrortypes:
MIRRNO equ 1
MIRRH equ 2
MIRRHV equ 3
  db MIRRNO, MIRRH, MIRRH, MIRRH
  db MIRRH, MIRRH, MIRRH
  db 0

; Drawing cells to tile map ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "floor_tilemap",ROM0

CELL_W = 2

;;
; Calculate the top row of the floor.  This is centered in the
; Game Boy screen, except at height 8 where it's 1 higher than
; expected because the back wall isn't drawn.
; Thus for floor heights 2, 3, 4, 5, 6, 7, 8, this is
; 7, 6, 5, 4, 3, 2, 0.

calc_floor_top_row::
  ; Calculate which tile row: A = y * 2 + 8 - height
  ld a,[floor_height]
  ; don't make room for back wall if already 8 cells tall
  cp 8
  jr nz,.notfullht
    inc a
  .notfullht:
  cpl
  add 9+1  ; A = 9 - height
  ld [floor_top_row],a
  ret

;;
; Sets HL to the tilemap address of the cursor
goto_tile_at_cursor:
  ld a,[cursor_y]
  ld d,a
  ld a,[cursor_x]
  ld e,a
  ; fall through

;;
; Sets HL to the tilemap address of cell (E, D)
; SCRN_BG + 32 * (y * 2 + top row) + (x * 2 + 10 - width)
; BCDE unchanged
goto_tile_e_d::
  ld a,[floor_top_row]
  add d
  add d

  ; fast mul by 32 to get which tile row
  swap a
  add a  ; save the carry!
  ld l,a
  ld a,high(SCRN_BG) >> 1
  rla
  ld h,a

  ; Seek to tile within row: A = x * 2 + 10 - width
  ld a,[floor_width]
  cpl
  add 10+1  ; A = 10 - width
  add e
  add e
  add l
  ld l,a
  ret

prepare_hDrawCell::
  call goto_tile_at_cursor
  ld d,h
  ld a,l

  ; Write first packet header to popslide_buf
  ld hl,popslide_used
  ld l,[hl]  ; now points into popslide_buf
  ld [hl+],a
  add 32
  ld e,a
  ld a,d
  ld [hl+],a
  jr nc,.noincde
    inc d
  .noincde:
  ld a,CELL_W - 1  ; 2 horizontal
  ld [hl+],a
  inc hl
  push de  ; Stack: bottom half address

  ; Find the shape
  push hl
  call floor_read_at_cursor
  pop hl
  ld b,a

  ; Calculate base tile ID (shades: 0, 4, 8, C; deadends: 40, 44, 48, 4C)
  add a
  add a
  ; If bits 2 (not round trip reachable) and 3 (entered) are both
  ; true, this is an entered dead end, drawn with tiles $40-$4F.
  ; Otherwise it's a regular cell, drawn with $00-$0F (plus arrows).
  if SHOW_UNENTERED_DEADENDS
    and $1C  ; save only deadend and shade bits
    add $30
  else
    and $3C  ; save only deadend, entered, and shade bits
    add $10
  endc
  and $4C

  ; construct 4 corners of tile
  ; E D
  ; C A
  ld e,a
  inc a
  ld c,a
  inc a
  ld d,a
  inc a

  ; Turn on tracks at sides of tiles
  bit 4,b
  jr z,.no_right_track
    set 4,d
    set 4,a
  .no_right_track:
  bit 5,b
  jr z,.no_left_track
    set 4,e
    set 4,c
  .no_left_track:
  bit 6,b
  jr z,.no_up_track
    set 5,e
    set 5,d
  .no_up_track:
  bit 7,b
  jr z,.no_down_track
    set 5,c
    set 5,a
  .no_down_track:
  ld b,a
  ld a,e
  ld [hl+],a
  ld a,d
  ld [hl+],a

  ; Write bottom row
  pop de
  ld a,e
  ld [hl+],a
  ld a,d
  ld [hl+],a
  ld a,CELL_W - 1
  ld [hl+],a
  inc hl
  ld a,c
  ld [hl+],a
  ld a,b
  ld [hl+],a
  ld a,l
  ld [popslide_used],a
  ret

gbc_prepare_cell_attr::
  call goto_tile_at_cursor
  ld d,h
  ld a,l

  ; Write first packet header to popslide_buf
  ld hl,popslide_used
  ld l,[hl]  ; now points into popslide_buf
  ld [hl+],a
  add 32
  ld e,a
  ld a,d
  ld [hl+],a
  jr nc,.noincde
    inc d
  .noincde:
  push de  ; Stack: bottom half address

  ld a,CELL_W  + 63  ; 2 horizontal
  ld [hl+],a
  push hl
  call floor_read_at_cursor
  pop hl
  and $03  ; A = color

  ; Calculate attribute: 1, 0, 2, 0
  rra    ; 0 0 1 1
  inc a  ; 1 1 2 2
  jr nc, .notgray
    xor a
  .notgray:
  ld [hl+], a
  ld b, a
  
  ; Bottom half same
  pop de
  ld a, e
  ld [hl+], a
  ld a, d
  ld [hl+], a
  ld a, CELL_W  + 63  ; 2 horizontal
  ld [hl+], a
  ld a, b
  ld [hl+], a

  ; Save new pointer
  ld a,l
  ld [popslide_used],a
  ret

draw_whole_floor::
  xor a
  .rowloop:
    ld [cursor_y],a

    ; Fill Popslide buffer with a row of cells
    xor a
    .tileloop:
      ld [cursor_x],a
      call prepare_hDrawCell
      ld a,[floor_width]
      ld b,a
      ld a,[cursor_x]
      inc a
      cp b
      jr nz,.tileloop
    call popslide_terminate_blit
    
    ld a, [hw_capability]
    rlca  ; bit 0 and CF true for GBC
    jr nc, .noGBCCellAttrs
      ldh [rVBK], a
      xor a
      .attrloop:
        ld [cursor_x],a
        call gbc_prepare_cell_attr
        ld a,[floor_width]
        ld b,a
        ld a,[cursor_x]
        inc a
        cp b
        jr nz,.attrloop
      call popslide_terminate_blit
      xor a
      ldh [rVBK], a
    .noGBCCellAttrs:

    ; And move to the next row
    ld a,[floor_height]
    ld b,a
    ld a,[cursor_y]
    inc a
    cp b
    jr nz,.rowloop
  ret

;;
; Forms an attribute map for Super Game Boy in lineImgBuf
sgb_form_floor_attr::
  ; Clear the background to 0
  ld hl, lineImgBuf
  ld [hl], $07*8+6  ; ATTR_CHR, packet count
  inc hl
  xor a
  ld [hl+], a        ; starting X: 0
  ld [hl+], a        ; starting Y: 0
  ld [hl], low(360)  ; number of 2-bit attributes
  inc hl
  ld [hl], high(360)
  inc hl
  ld [hl+], a        ; row major
  ld d, a
  push hl            ; Stack: address of top of attribute table
  cpl                ; color 3 for out-of-bounds tiles
  ld c, 90
  call memset_tiny

  ld a, [floor_top_row]
  ld b, a
  add a
  add a
  add b
  ld e, a     ; DE: offset of first row within attribute table
  pop hl
  add hl, de  ; HL: offset of first nonborder row in attribute table
  ld de, floor_map
  ld a, [floor_height]
  ld b, a

  .rowloop:
    ; Regmap now:
    ; B: remaining floor rows
    ; DE: pointer into floor_map
    ; HL: destination
    push bc
    push de
    push hl
    ; Stack: destination row start, floor map row start, remaining floor row count
    ld b, 1  ; B is a circular buffer to collect bits
    call .writelrborder

    ; Write attributes for floor cells
    ld a, [floor_width]
    ld c, a
    .tileloop:
      ld a, [de]
      inc de
      and $03

      ; Calculate attribute: 1, 0, 2, 0
      rra    ; 0 0 1 1
      inc a  ; 1 1 2 2
      jr nc, .notgray
        xor a
      .notgray:
      call .write2bits
      call .write2bits
      dec c
      jr nz, .tileloop
    call .writelrborder

    ; Duplicate the attribute row
    pop de
    ld c, 5
    .cpy:
      ld a, [de]
      ld [hl+], a
      inc de
      dec c
      jr nz, .cpy

    ; move to next row of floor
    pop de
    ld a, e
    or 7
    ld e, a
    inc de
    pop bc
    dec b
    jr nz, .rowloop
  ret

.writelrborder:
  ld a, [floor_width]
  cpl
  sub (255 - 10)
  ld c, a
  ld a, 3
.write2bitstimesC:
  call .write2bits
  dec c
  jr nz, .write2bitstimesC
  ret
.write2bits:
  rrca   ; A: a:aXXXXXXb
  rrca   ; A: b:baXXXXXX
  rlca   ; A: b:aXXXXXXb
  rl b
  rlca   ; A: a:XXXXXXba
  rl b
  ret nc
  ld [hl], b
  inc hl
  ld b, 1
  ret

draw_floor_border::
  ld a,[floor_height]
  cp 8
  jr nc,.no_back_wall
    ; Bottom row
    ld bc,($71 << 8)|(-33 & $FF)
    call .do_one_top_row
    ; Top row
    ld bc,($70 << 8)|(-65 & $FF)
    call .do_one_top_row
  .no_back_wall:

  ; Left wall
  ld de,0
  call goto_tile_e_d
  dec hl
  ld a,[floor_height]
  ld c,a
  ld a,$77
  call .do_one_side_wall

  ld a,[floor_width]
  ld e,a
  call goto_tile_e_d
  ld a,$76
  ; fall through

;;
; @param HL top tile address
; @param C height in tile pairs
; @param A tile ID
.do_one_side_wall:
  push af
  ld a, [hw_capability]
  rlca
  jr nc, .side_notGBC
    ldh [rVBK], a
    push bc
    push hl
    ld a, 3  ; walls palette
    call .do_one_side_wall_plane
    pop hl
    pop bc
    xor a
    ldh [rVBK], a
  .side_notGBC:
  pop af
.do_one_side_wall_plane:
  ld b,c
  ld de,32
  .sidewallloop:
    ld [hl],a
    add hl,de
    ld [hl],a
    add hl,de
    dec b
    jr nz,.sidewallloop
  ret

;;
; @param B top left tile ID
; @param C signed amount to add to HL: -33 or -65
.do_one_top_row:
  ld de,0
  call goto_tile_e_d
  dec d
  ld e,c
  add hl,de

  ld a, [hw_capability]
  rlca
  jr nc, .toprow_notGBC
    ldh [rVBK], a
    push hl
    ld a, [floor_width]
    inc a
    add a
    ld c, a
    ld a, 3  ; walls palette
    call memset_tiny
    pop hl
    xor a
    ldh [rVBK], a
  .toprow_notGBC:

  ; Top left corner
  ld a,b
  ld [hl+],a
  ; Body of top
  ld a,[floor_width]
  ld c,a
  ld a,b
  add 2
  .toprowloop:
    ld [hl+],a
    ld [hl+],a
    dec c
    jr nz,.toprowloop
  ; Top right corner
  ld a,b
  add 4
  ld [hl+],a
  ret

; Exit indication ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

prepare_exit_door::
  ; Only maps not taller than 8 cells have an exit door.
  ; For taller maps, all the top cells are exits, and arrows
  ; at the top indicate that the exit is open.
  ld a,[floor_height]
  cp 8
  jr nc,prepare_exit_arrows

  ; Door is drawn 2 cells above exit cell
  ld d,0
  ld a,[exit_x]
  ld e,a
  call goto_tile_e_d
  ld de,-32*2
  add hl,de
  ld d,h
  ld a,l

  ; Form top row packet
  ld hl,popslide_used
  ld l,[hl]
  ld [hl+],a
  add 32
  ld e,a
  ld a,d
  ld [hl+],a
  jr nc,.noewrap
    inc d
  .noewrap:
  ld a,CELL_W - 1
  ld [hl+],a
  inc hl

  ; Closed  Open
  ; 78 7A   7C 7E
  ; 79 7B   7D 7F
  ld a,[exit_open]
  or a
  ld a,$78
  jr z,.use_open_tile
    ld a,$7C
  .use_open_tile:
  ld [hl+],a
  add 2
  ld [hl+],a
  dec a
  ld b,a

  ; Second row
  ld a,e
  ld [hl+],a
  ld a,d
  ld [hl+],a
  ld a,CELL_W - 1
  ld [hl+],a
  inc hl
  ld a,b
  ld [hl+],a
  add 2
  ld [hl+],a
  ld a,l
  ld [popslide_used],a
  ret

prepare_exit_arrows:
  ld a,[exit_open]
  or a
  ret z
  ld de,0
  call goto_tile_e_d
  ld d,h
  ld e,l
  ld hl,popslide_used
  ld l,[hl]

  ; Draw arrow on left
  ld a,e
  sub 2
  ld [hl+],a
  ld a,d
  ld [hl+],a
  xor a  ; $00: 1 byte
  ld [hl+],a
  ld a,$6E  ; arrow tile
  ld [hl+],a

  ; Draw arrow on right
  ld a,[floor_width]
  add a
  inc a
  add e
  ld [hl+],a
  ld a,d
  ld [hl+],a
  xor a
  ld [hl+],a
  ld a,$6E  ; arrow tile
  ld [hl+],a

  ld a,l
  ld [popslide_used],a
  ret
