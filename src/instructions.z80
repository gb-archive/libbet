include "src/hardware.inc"
include "src/global.inc"

LF equ $0A

section "instructions", ROM0

first_vwf_tileid = 256 - 8 * 14

do_instructions::
  call lcd_off
  xor a
  ld [cursor_x],a
  ld de,_SCRN0
  ld h,$80
  ld bc,32*18
  call memset
  ld hl,$8800
  ld c,16
  xor a
  call memset_tiny

  ld de,18
  ld b,16
  ld a,first_vwf_tileid
  ld hl,_SCRN0+32*1+3
  .filltilemaploop:
    ld c,14
    call memset_inc
    add hl,de
    dec b
    jr nz,.filltilemaploop

.page:

  ; Draw the text
  call lcd_off
  ld a,[cursor_x]
  ld de,pages
  call de_index_a
  ld de,CHRRAM0 + first_vwf_tileid * 16
  .lineloop:
    push hl
    call vwfClearBuf
    pop hl
    push de
    ld b,0
    call vwfPuts
    pop de
    push hl
    ld h,d
    ld l,e
    ld bc,14
    call vwfPutBuf03_lenC
    ld d,h
    ld e,l
    pop hl
    ld a,[hl+]
    or a
    jr nz,.lineloop

  call vwfClearBuf
  .clrrestloop:
    ld h,0
    ld bc,14*16
    call memset
    ld hl,$10000-(CHRRAM0 + (224 + first_vwf_tileid) * 16)
    add hl,de
    jr nc,.clrrestloop

  ld a,%00001100
  ldh [rBGP],a
  ld a,LCDCF_ON|LCDCF_BGON|LCDCF_BG8800|LCDCF_BG9800
  ld [vblank_lcdc_value],a
  ldh [rLCDC],a

.loop:
  call wait_vblank_irq
  call read_pad
  ld a,[new_keys]
  ld b,a
  ld a,[cursor_x]
  ld c,a

  ; Page movement
  ld a,PADF_RIGHT|PADF_A
  and b
  ld a,c
  jr z,.notRight
  cp (pages_end-pages)/2-1
  jr nc,.notRight
    inc a
  .notRight:
  bit PADB_LEFT, b
  jr z,.notLeft
  or a
  jr z,.notLeft
    dec a
  .notLeft:
  cp c
  jr z,.notRedraw
    ld [cursor_x],a
    jp .page
  .notRedraw:

  bit PADB_START,b
  ret nz
  bit PADB_A,b
  jr z,.loop
  ; A was pressed; return
  ld a,c
  cp (pages_end-pages)/2-1
  jr c,.loop
  ret

section "instructionstxt", ROM0

pages:
 dw page1txt, page2txt, page3txt
pages_end:

page1txt:
 db "Libbet and the",LF
 db "Magic Floor",LF
 db "v0.01 (2018-10-16)",LF
 db LF
 db $82," 2002, 2012 Martin Korth",LF
 db $82," 2018 Damian Yerrick",LF
 db LF
 db "One day, Libbet was",LF
 db "rearranging her basement",LF
 db "when she discovered a",LF
 db "passage to an empty",LF
 db "hall whose floor had a",LF
 db "peculiar pattern. She",LF
 db "rolled in to investigate.",0
page2txt:
 db "The floor tiles have four",LF
 db "shades. Libbet can roll",LF
 db "or jump between tiles of",LF
 db "the same shade.",LF
 db "She can also roll or jump",LF
 db "onto the next brighter",LF
 db "shade or from white to",LF
 db "black. This leaves a track.",LF
 db "Tiles with no exits",LF
 db "contain a trap door that",LF
 db "leads to the entrance.",LF
 db "Leave 90 percent of",LF
 db "possible tracks and roll",LF
 db "to the exit at top to win.",0
page3txt:
 db "Controls",LF
 db LF
 db $86,$87,$85,$84,": Roll",LF
 db "A + ",$86,$87,$85,$84,": Jump",LF
 db LF
 db "Press Start Button",0

attract_map::
 db 3, 0, 0, 1  ; 4
 db 2, 2, 1, 3  ; 3
 db 0, 1, 0, 3  ; 2
 db 3, 2, 2, 1  ; 1
 ;  a  b  c  d

attract_moves:
; Start at c1
; roll/jump on same color:
; b1 b3 a3 b3 b1 c1
; roll/jump on different colors:
; b1 a1 a2 b2 b3 d3 d2 c2 c3 a3
; dead ends:
; a4 b4 d4 (warp to c1) a1 a2 c2 c4 d4 (warp to c1)
; finish the job:
; b1 b3 a3 a4 c4 c3 c1 a1 a2 c2 b2 b1 a1 a2 b2 b3 a3 a4 b4 b2 b3 a3 a4 c4 exit

