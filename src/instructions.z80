;
; Instruction screen and attract mode for GB port of Magic Floor
;
; Copyright 2002, 2012 Martin Korth [concept]
; Copyright 2018, 2020 Damian Yerrick [program]
;
; This software is provided 'as-is', without any express or implied
; warranty.  In no event will the authors be held liable for any damages
; arising from the use of this software.
; 
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
; 
; 1. The origin of this software must not be misrepresented; you must not
;    claim that you wrote the original software. If you use this software
;    in a product, an acknowledgment in the product documentation would be
;    appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
;    misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;
include "src/hardware.inc"
include "src/global.inc"

LF equ $0A
FF equ $0C
COPR equ $7F

section "instructions", ROM0

; Sets up a 128-pixel-wide VWF window on rows 1 to B and B+2 to 17
; @param B number of rows before first blank row
vwf_page_init::
  ld a,16  ; calculate num
  sub b
  ld c,a
  push bc

  call lcd_off
  xor a
  ldh [rSCX],a
  ld a,4
  ldh [rSCY],a
  ld a,%00001100
  call set_bgp

  ; Clear CHR banks 1 and 2
  ld de,$8800
  ld h,e
  ld bc,256*16
  call memset
  ; and the tilemap immediately following
  ld h,$6F
  ld bc,32*19
  call memset
  call clear_gbc_attr

  ; Fill top rows, centered horizontally and starting on
  ; the second row of the first BG
  pop bc
  push bc
  ldxy hl, 10 - vwf_page_width/2, 1, _SCRN0
  ld de,32 - vwf_page_width
  ld a,vwf_page_first_tileid
  call .filltilemaploop
  ld bc,32  ; skip a row
  add hl,bc
  pop bc
  ld b,c
  .filltilemaploop:
    ld c,vwf_page_width
    call memset_inc
    add hl,de
    dec b
    jr nz,.filltilemaploop

  ret

do_instructions::
  ld b, 28
  call sgb_set_palette_b
  ld b, 3
  call vwf_page_init
  xor a
  ld [cursor_x],a
.page:

  ; Draw the text
  call lcd_off
  ld a,[cursor_x]
  ld de,pages
  call de_index_a
  ld de,CHRRAM0 + vwf_page_first_tileid * 16
  ; HL: text pointer; DE: VRAM pointer
  .lineloop:
    push hl
    call vwfClearBuf
    pop hl
    push de
    ld b,0
    call vwfPuts
    pop de
    push hl
    ld h,d
    ld l,e
    ld bc,vwf_page_width
    call vwfPutBuf03_lenC
    ld d,h
    ld e,l
    pop hl
    ld a,[hl+]
    or a
    jr nz,.lineloop

  call vwfClearBuf
  jr .clrresttest
  .clrrestloop:
    ld h,0
    ld bc,vwf_page_width*16
    call memset
  .clrresttest:
    ld hl,$10000-(CHRRAM0 + ((16 * vwf_page_width) + vwf_page_first_tileid) * 16)
    add hl,de
    jr nc,.clrrestloop

  ld a,LCDCF_ON|LCDCF_BGON|LCDCF_BG8800|LCDCF_BG9800
  ld [vblank_lcdc_value],a
  ldh [rLCDC],a

.loop:
  call wait_vblank_irq
  call audio_update
  call read_pad
  ld a,[new_keys]
  ld b,a
  ld a,[cursor_x]
  ld c,a

  ; Page movement
  ld a,PADF_RIGHT|PADF_A
  and b
  ld a,c
  jr z,.notRight
  cp (pages_end-pages)/2-1
  jr nc,.notRight
    inc a
  .notRight:
  bit PADB_LEFT, b
  jr z,.notLeft
  or a
  jr z,.notLeft
    dec a
  .notLeft:
  cp c
  jr z,.notRedraw
    ld [cursor_x],a
    jp .page
  .notRedraw:

  ld a,PADF_START|PADF_SELECT
  and b
  ret nz
  bit PADB_A,b
  jr z,.loop
  ; A was pressed; return
  ld a,c
  cp (pages_end-pages)/2-1
  jr c,.loop
  ret

section "instructionstxt", ROM0

pages:
 dw page1txt  ;, page2txt, page3txt
pages_end:

page1txt:
 db LF
 db "Libbet and the Magic Floor",LF
 db "v0.06wip (2020-05-03)",LF
 ; Row skipped because B=3
 ; Can't cut 1 line here because "rearranging" and "passage" are so long
 db "One day, Libbet was",LF
 db "rearranging her basement",LF
 db "when she discovered a",LF
 db "passage to an empty",LF
 db "hall whose floor had a",LF
 db "peculiar pattern. She",LF
 db "rolled in to investigate.",LF
 db LF
 db "Direction: roll",LF
 db "A+direction: jump",LF
 db LF
 db "Select: demo | Start: play",0


; Attract mode ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

STATUS_BLANK equ $F7

section "attractstate", WRAM0
attract_src_ptr:: ds 2
attract_vwf_tile_id:: ds 1  ; $80 through $BF
attract_vwf_x:: ds 1  ; X = $FF and tile id < $C0 means a clear is in progress

; While writing text:
; attract_vwf_x in 0-7, attract_vwf_tile_id in $80-$BF
; While clearing:
; attract_vwf_x = $FF, attract_vwf_tile_id in $80-$BF

section "attractcode", ROM0

attract_init_vram::
  ; Extend status bar
  ldxy hl, 0, 12
  ld a,STATUS_BLANK
  ld c,32*4
  call memset_tiny

  ; Set up VWF canvas on extended status bar
  ld a,$80
  ldxy hl, 2, 12
  ld bc,$0410
  ld de,16
  .msiloop:
    push bc
    call memset_inc
    pop bc
    add hl,de
    dec b
    jr nz,.msiloop

  ; Clear VWF canvas
  ld hl, CHRRAM1
  xor a
  ld b, a
  .clrcanvasloop:
    rept 4
      cpl
      ld [hl+], a
    endr
    inc b
    jr nz, .clrcanvasloop

  ; Seek to start of attract mode script
  ld hl,attract_src_ptr
  ld a,low(attract_script)
  ld [hl+],a
  ld a,high(attract_script)
  ld [hl+],a

  ; Initialize the text cursor
  ld a,$81
  ld [hl+],a  ; starting tile id
  xor a  ; starting pixel within tile
  ld [hl],a
  jp vwfClearBuf

;;
; Does one-eighth of the job of clearing the VWF area.
; Appends two 32-byte +32 run packets at $880x, $881x
; @param attract_vwf_tile_id the tile ID ($80, $88, $90, ..., $B8)
attract_clear_half_line:
  ld hl,popslide_used
  ld l,[hl]
  ld a,l
  add 4 * 2
  ld [popslide_used],a

  ; Find the starting pointer
  ld a,[attract_vwf_tile_id]  ; $80, $88, $A0, ..., $B8 becomes 
  rra
  rra      ; $20, $22, $24, ..., $2E
  sub $0F  ; $11, $13, $15, ..., $1F
  ld e, a
  call .one_packet
  ld a, e
  sub $10

;;
; Private entry: Writes one run of 32
.one_packet:
  ld [hl+], a
  ld a, $88
  ld [hl+], a
  ld a, $DF  ; bit 7: +32; bit 6: run; length 31+1
  ld [hl+], a
  xor a
  ld [hl+], a
  ret

;;
; Writes the current and next VWF tile to the Popslide buffer
; @param attract_vwf_tile_id the tile number ($80-$BF); only one tile
; gets written for $xF
attract_push_vwf_tiles:
  ld hl,popslide_used
  ld l,[hl]
  ld a,[attract_vwf_tile_id]
  swap a
  ld c,a
  ld b,$0F
  cp $F0  ; If on the last tile of a line, write only one tile
  jr nc,.only_one_tile
    ld b,$1F
  .only_one_tile:
  and $F0
  ld [hl+],a  ; header: address low
  xor c
  or high(CHRRAM1)
  ld [hl+],a  ; header: address high
  ld a,b
  ld [hl+],a  ; header: length - 1
  inc l       ; skip unused header byte

  inc b  ; convert to length / 2
  srl b
  ld de,lineImgBuf
  .loop:
    ld a,$FF
    ld [hl+],a
    ld a,[de]
    inc e
    ld [hl+],a
    dec b
    jr nz,.loop

  ld a,l
  ld [popslide_used],a
  ret

;;
; Process one byte of the attract mode script.
; @return A: move to try
attract_update::
  ld a,[attract_vwf_x]
  inc a
  jr nz,.not_in_clear
    call attract_clear_half_line
    ld a,[attract_vwf_tile_id]
    add 8
    cp $C0
    jr c,.clear_have_tile_id
      ld a,8
      ld [attract_vwf_x],a
      swap a  ; ld a,$80
    .clear_have_tile_id:
    ld [attract_vwf_tile_id],a
    xor a
    ret
  .not_in_clear:

  ld hl,attract_src_ptr
  ld a,[hl+]
  ld h,[hl]
  ld l,a
  ld a,[hl+]

  ; If at the end of the script, return a Start press
  or a
  jr nz,.not_end
    ld a,PADF_START
    ret
  .not_end:

  ; If not at the end, write back pointer
  ld c,a
  ld a,l
  ld [attract_src_ptr],a
  ld a,h
  ld [attract_src_ptr+1],a

  ld a,c
  cp $09
  jr nc,.not_direction
    dec c
    ld a,c
    and MOVEF_DIRECTION
    ld b,a
    ld a,$10
    jr z,.nokeyshift
    .keyshiftloop:
      rlca
      dec b
      jr nz,.keyshiftloop
    .nokeyshift:
    bit MOVEB_JUMP,c
    ret z
    inc a
    ret
  .not_direction:

  cp $10
  jr c,.not_letter
    ld a,[attract_vwf_x]
    ld b,a
    ld a,c
    push bc  ; Stack: X and glyph ID
    call vwfPutTile
    call attract_push_vwf_tiles

    ; Move pen forward by the glyph's width
    pop bc
    ld de,vwfChrWidths-" "
    ld a,c
    add e
    ld e,a
    jr nc,.gwnowrap
      inc d
    .gwnowrap:
    ld a,[de]
    add b

    ; See if the pen has entered a new tile
    ld bc,8
    cp c
    jr c,.letter_not_new_tile
      sub c
      ; Shift tile buffer left by 8
      push af
      ld hl,attract_vwf_tile_id
      inc [hl]
      ld hl,lineImgBuf+8
      ld de,lineImgBuf
      call memcpy
      ld c,8
      ld h,b
      call memset
      pop af
    .letter_not_new_tile:
    ld [attract_vwf_x],a
  .not_formfeed:
    xor a
    ret
  .not_letter:

  cp LF
  jr nz,.not_linefeed
    ; Move to the next row down
    ld a,[attract_vwf_tile_id]
    and $F0
    add $11
    ld [attract_vwf_tile_id],a
    xor a
    .set_vwf_x_clear_linebuf:
    ld [attract_vwf_x],a

    ; And clear the relevant part of the line buffer
    xor a
    ld c,16
    ld hl,lineImgBuf
    jp memset_tiny
  .not_linefeed:

  cp FF
  jr nz,.not_formfeed
    ; Go to screen clearin' mode
    ld a,$80
    ld [attract_vwf_tile_id],a
    ld a,$FF
    jr .set_vwf_x_clear_linebuf

; Attract mode map ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "attractdata", ROM0

attract_map::
 db 3, 0, 0, 1  ; 4
 db 2, 2, 1, 3  ; 3
 db 0, 1, 0, 3  ; 2
 db 3, 2, 2, 1  ; 1
 ;  a  b  c  d

; This floor has 20 points and its solution is as follows:
; _____ _____ _| |__ _____
;|     |     |     |     |
;|  3 >|  0 >|  0 >| (1) |
;|__v__|__v__|__ __|__ __|
;|  ^  |     |     |     |
;|  2  |  2 >|< 1  |  3  |
;|__v__|__ __|__v__|__ __|
;|     |  ^  |  ^  |     |
;|  0 >|  1  |< 0  |< 3  |
;|__ __|__v__|__ __|__ __|
;|  ^  |     |     |     |
;|  3  |< 2  |< 2  | X1X |
;|__ __|__ __|__ __|__ __|
;
; This route is used when testing Restless. It differs from the
; attract mode route in not visiting the dead end.
; AL U AR L D  L U R U AR
; D L U AL U   R R D AD L
; AU L U AD R  U L AD U R
; U L U R AD   U L U R R
; U

; Attract mode script ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "attractmap", ROM0

; AM_constants are one greater than the underlying move code
AM_R equ $01
AM_L equ $02
AM_U equ $03
AM_D equ $04
AM_RR equ $05
AM_LL equ $06
AM_UU equ $07
AM_DD equ $08

;;
; Bytecode format
; $00: End
; $01-$08: Move x-1
; $0A: New line
; $0C: Clear page
; $10+: Draw one VWF glyph
attract_script:
; Start at c1
; roll/jump on same color:
  db "The floor tiles have four",LF
  db "shades. Libbet can roll",LF
  db "or jump between tiles of",LF
  db "the same shade."
; b1 b3 a3 b3 b1 c1
  db AM_L, AM_UU, AM_L, AM_R, AM_DD, AM_R

; repeat same moves while explaining controls
  db FF
  db "To roll, press a direction",LF
  db "on the Control Pad.",LF
  db "To jump, hold the A Button",LF
  db "and press a direction."
  db AM_L, AM_UU, AM_L, AM_R, AM_DD, AM_R

; so far: 0/20 (0%)
; roll/jump on different colors:
  db FF
  db "She can also roll or jump",LF
  db "onto the next brighter",LF
  db "shade or from white to",LF
  db "black. This leaves a track."
; b1 a1+ a2+ b2+ b3+ d3+ d2 c2+ c3+ a3+
  db AM_L, AM_L, AM_U, AM_R, AM_U, AM_RR, AM_D, AM_L, AM_U, AM_LL
; so far: 8/20 (40%)

; dead ends:
  db FF
  db "Tiles with no exits",LF
  db "contain a trap door that",LF
  db "leads to the entrance."
; a4+ b4+ d4+ [c1] a1+ a2[+] c2 c4+ d4 [c1]
  db AM_U, AM_R, AM_RR
  db AM_LL, AM_U, AM_RR, AM_UU, AM_R
; result: 13/20 (65%)

; finish the job:
  db FF
  db "Leave 90 percent of",LF
  db "possible tracks and roll",LF
  db "to the exit at top to win."
; a1 a2 c2 c4 c3+ c1+
  db AM_LL, AM_U, AM_RR, AM_UU, AM_D, AM_DD
; b1 b3 a3 a4 a2+ c2 b2+ b1+
  db AM_L, AM_UU, AM_L, AM_U, AM_DD, AM_RR, AM_L, AM_D
; b3 a3 a4 c4 exit
  db AM_UU, AM_L, AM_U, AM_RR, AM_U

; this gets us to 18/20 (90%)
  db $00
