;
; Combo counter for GB port of Magic Floor
;
; Copyright 2019 Damian Yerrick
;
; This software is provided 'as-is', without any express or implied
; warranty.  In no event will the authors be held liable for any damages
; arising from the use of this software.
; 
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
; 
; 1. The origin of this software must not be misrepresented; you must not
;    claim that you wrote the original software. If you use this software
;    in a product, an acknowledgment in the product documentation would be
;    appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
;    misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;

; When combo breaks, the previous combo rises off the status bar

MIN_PREVCOMBO equ 3
PREVCOMBO_X equ 8
PREVCOMBO_START_Y equ 128
PREVCOMBO_START_DY equ -20
PREVCOMBO_FRAMES equ 80

section "prevcomboram", WRAM0
prevcombo_timeleft: ds 1
prevcombo_dy: ds 1
prevcombo_y: ds 1
prevcombo_digits: ds 1

section "prevcombo", ROM0
prevcombo_update::
  ; decrement this particle's life
  ld hl, prevcombo_timeleft
  ld a, [hl]
  or a
  ret z
  dec a
  ld [hl+], a

  ; Add 1px of downward velocity every fourth frame if still rising
  ld a, [hl]
  cp $80  ; carry 0 if negative or 1 if positive
  sbc $FF  ; add 1 - carry
  ld [hl+], a

  ; Add a fraction of velocity to Y position
  and $FC
  sra a
  sra a
  sra a
  adc [hl]
  ld [hl+], a
  ld b, a     ; B = Y position
  ld c, [hl]  ; C = digits

  ; Draw each digit
  ld hl, oam_used
  ld l, [hl]  ; HL = OAM pointer
  ld a, c
  cp $10
  jr c, .no_tens
    swap c
    ld e, PREVCOMBO_X + 8
    call .onedigit
    swap c
  .no_tens:
  ld e, PREVCOMBO_X + 16
  call .onedigit
  ld a, l
  ld [oam_used], a
  ret

.onedigit:
  ; Y, X coords of top sprite
  ld a, b
  ld [hl+], a
  ld a, e
  ld [hl+], a

  ; Tile number and attribute of top sprite
  ld a, c
  and $0F
  add a
  add SPRITE_BIG_DIGIT_BASE
  ld [hl+], a
  inc a
  ld d, a
  xor a
  ld [hl+], a

  ; Y, X coords of bottom sprite
  ld a, b
  add 8
  ld [hl+], a
  ld a, e
  ld [hl+], a

  ; Tile number and attribute of bottom sprite
  ld a, d
  ld [hl+], a
  xor a
  ld [hl+], a
  ret

prevcombo_init::
  xor a
  ld [prevcombo_timeleft], a
  ret

prevcombo_set::
  cp MIN_PREVCOMBO
  ret c
  call bcd8bit_baa
  ld hl, prevcombo_digits
  ld [hl-], a
  ld a, PREVCOMBO_START_Y + 16
  ld [hl-], a
  ld a, PREVCOMBO_START_DY
  ld [hl-], a
  ld a, PREVCOMBO_FRAMES
  ld [hl], a
  ld a, 10
  jp audio_play_fx  ; oh no, combo stopped!
